; Написать резидентную программу под DOS, которая будет каждую секунду менять
; скорость автоповтора ввода символов в циклическом режиме, от самой медленной до
; самой быстрой.
; Через машинный код EA команды far JMP, сохранив адрес перехода напрямую в
; непосредственный операнд команды


.MODEL TINY
.186

CSEG SEGMENT para public 'CODE'
	ORG 100h 					;отступ для заголовка размер PSP для COM программы
	assume CS:CSEG, DS:CSEG
main:
	jmp init
	
	speed db 11111b  ;	2 символа в секунду
	tact db 17

breaking:
	push AX ;  сохраняем значения регистров, которые могут поменяться в процессе выполнения процедуры
	
	mov al, 20h  ;(код прерывания таймера).
	out 20h, al ;- отправка команды контроллеру прерываний.
	
	inc tact
	cmp tact, 18  
	jne end_breaking
	
	mov tact, 0
	
	mov al, 0F3h  ;команда F3h отвечает за параметры режима автоповтора нажатой клавиши ; '0' в начале числа можно и не писать, но тогда MASM будет воспринимать это как метку, потому что она начинается с буквы
	out 60h, al    ; порт 60h предназначен для работы с клавиатурой и обычно принимает пары байтов последовательно: первый - код команды, второй - данные
	
	mov al, speed  ; устанавливаем скорость автоповтора ввода
	out 60h, al
	
	dec speed
	and speed, 00011111b ; установка младших 5 битов значения speed в 0.
		
	end_breaking:
		pop AX 
		
		JUMP db 0eah 	; код jmp   
		ARG_1 dw 0
		ARG_2 dw 0
	
init:
	mov ah, 35h		; получить адрес обработчика прерываний
	mov al, 08h		; таймер
	int 21h
	
	mov ARG_1, bx ;(в ES - сегментная часть адреса, в BX - смещение)  сохраняем адрес обработчика прерывания 
	mov ARG_2, es
	
	mov dx, offset breaking ;выход если програм запущена 2 раз
	cmp bx, dx
	je exit
	
	mov ah, 25h		; установить адрес нового обработчика
	mov al, 08h
	int 21h
	
	mov dx, offset init  ; начиная с init можно удалить
	int 27h ;Для завершения программы с сохранением в памяти в DOS 
	
exit:
	pusha  ; сохраняет значения регистров общего назначения в стеке.
	
	mov dx, ES:ARG_1
	mov ax, ES:ARG_2
	mov ds, ax
	
	mov ah, 25h ;замены вектора прерывания
	mov al, 08h ;устанавливает номер прерывания для таймера.
	int 21h
        				; для установки характеристик режима автоповтора в порт 60h необходимо записать код команды 0F3h, затем байт, определяющий характеристики режима
  mov al, 0F3h
	out 60h, al
	
	mov al, 0
	out 60h, al ; устанавливаем период автоповтора 30.0, задержку включения режима автоповтора 250 мс (восстанавливаем дефолтные значения)
    
  popa ;восстанавливает значения регистров общего назначения из стека.
    
  mov ah, 49h 	; освободить память
	int 21h
    
  mov ax, 4c00h
  int 21h
CSEG ENDS
END main